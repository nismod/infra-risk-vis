import os
import subprocess

import geopandas
import pandas


# Example config
# - override by running:
#    snakemake --cores=1 --configfile=config_local.yml all
configfile: "config.yml"


# Read further configuration data from CSV
hazard_layers = pandas.read_csv(config["hazard_layers"])
network_layers = pandas.read_csv(config["network_layers"])
network_tilelayers = pandas.read_csv(config["network_tilelayers"])
damage_rp_files = pandas.read_csv(config["damage_rp_files"])
damage_exp_files = pandas.read_csv(config["damage_exp_files"])
adaptation_files = pandas.read_csv(config["adaptation_files"])


wildcard_constraints:
    layer="[^/]+",


# Run all file-base jobs, after load_to_database
rule all:
    input:
        expand(
            "../tileserver/vector/data/{layer}.mbtiles", layer=network_tilelayers.layer
        ),
        expand("../tileserver/raster/data/{layer}.tif", layer=hazard_layers.key),


# Prerequisite, not fully traced by file dependency
#   snakemake --cores=all load_to_database
rule load_to_database:
    input:
        expand("logs/network/{layer}.txt", layer=network_layers.ref),
        expand("logs/damages_rp/{layer}.txt", layer=damage_rp_files.ref),
        expand("logs/damages_exp/{layer}.txt", layer=damage_exp_files.ref),
        expand("logs/damages_npv/{layer}.txt", layer=network_layers.damage_ref),
        expand(
            "logs/adaptation/{hazard}/{layer}.txt",
            zip,
            hazard=adaptation_files.hazard,
            layer=adaptation_files.damage_ref,
        ),


rule feature_layers_to_db:
    input:
        config["network_tilelayers"],
    output:
        "logs/feature_layers.txt",
    script:
        "./scripts/feature_layers_to_db.py"


rule networks_to_db:
    """Read from source directory to database
    """
    input:
        "logs/feature_layers.txt",
    output:
        "logs/network/{layer}.txt",
    script:
        "./scripts/networks_to_db.py"


rule damages_rp_to_db:
    input:
        fname=f"{config['analysis_data_dir']}/vis/{{layer}}_rp_all.parquet",
    output:
        "logs/damages_rp/{layer}.txt",
    script:
        "./scripts/damages_rp_to_db.py"


rule damages_exp_to_db:
    input:
        expected=f"{config['analysis_data_dir']}/vis/{{layer}}_exp_all.parquet",
    output:
        "logs/damages_exp/{layer}.txt",
    script:
        "./scripts/damages_exp_to_db.py"


rule damages_npv_to_db:
    input:
        npv=f"{config['analysis_data_dir']}/results/loss_damage_npvs/{{layer}}_EAD_EAEL_npvs.csv",
        uid=f"{config['analysis_data_dir']}/processed_data/networks_uids/id_lookups/{{layer}}_ids.parquet",
    output:
        "logs/damages_npv/{layer}.txt",
    script:
        "./scripts/damages_npv_to_db.py"


rule adaptation_to_db:
    input:
        avoided_risk=f"{config['analysis_data_dir']}/results/adaptation_benefits_costs_bcr/{{hazard}}_{{layer}}_adaptation_costs_avoided_EAD_EAEL.csv",
        uid=f"{config['analysis_data_dir']}/processed_data/networks_uids/id_lookups/{{layer}}_ids.parquet",
    output:
        "logs/adaptation/{hazard}/{layer}.txt",
    script:
        "./scripts/adaptation_to_db.py"


rule db_to_geojsonseq:
    """Load from database to GeoJSONSeq
    """
    output:
        "vector/{layer}.geojsonl",
    script:
        "scripts/db_to_geojsonseq.py"


rule geojsonseq_to_vector_tiles:
    input:
        "vector/{layer}.geojsonl",
    output:
        "../tileserver/vector/data/{layer}.mbtiles",
    run:
        layer = get_tilelayer(wildcards.layer, network_tilelayers)

        max_zoom = 20
        point_polygon_zoom_switch = 13

        if layer.spatial_type == "line":
            options = [
                "--drop-densest-as-needed",
                "--minimum-zoom=3",
                f"--maximum-zoom={max_zoom}",
            ]
        elif layer.spatial_type == "polygon":
            options = [
                "--drop-smallest-as-needed",
                f"--minimum-zoom={point_polygon_zoom_switch}",
                f"--maximum-zoom={max_zoom}",
            ]
        else:
            options = [
                "--drop-densest-as-needed",
                "-Bg",
                f"--maximum-zoom={max_zoom}",
                "--full-detail=12"
            ]

        subprocess.run(
            [
                "tippecanoe",
                "--use-attribute-for-id=uid",
                "--read-parallel",
                f"--output={output}",
                f"--layer={wildcards.layer}",
            ]
            + options
            + ["--force", f"{input}"],
            check=True
        )

        if layer.spatial_type == "polygon":
            # create points as centroids
            input_basename = os.path.basename(str(input))
            input_layer = input_basename.replace(".geojsonl", "")
            tmp_output = f"vector/points/{input_basename}"
            subprocess.run(
                [
                    "ogr2ogr",
                    "-sql",
                    f"SELECT ST_Centroid(geometry), * FROM {input_layer}",
                    "-dialect",
                    "sqlite",
                    tmp_output,
                    str(input)
                ],
                check=True
            )
            subprocess.run(
                [
                    "tippecanoe",
                    "--use-attribute-for-id=uid",
                    "--read-parallel",
                    f"--output={output}",
                    f"--layer={wildcards.layer}",
                    "--drop-densest-as-needed",
                    "--minimum-zoom=3",
                    f"--maximum-zoom={point_polygon_zoom_switch - 1}",
                    "--force", f"{tmp_output}"
                ],
                check=True
            )


def get_tilelayer(layer_name, network_tilelayers):
    try:
        return network_tilelayers[network_tilelayers.layer == layer_name].iloc[0]
    except IndexError as e:
        print(f"Could not find {layer_name} in tilelayers.")
        raise e


rule extract_regions:
    input:
        f"{config['analysis_data_dir']}/input/admin-boundaries/gadm36_levels.gpkg",
    output:
        "regions/regions_{level,level\d+}.geojsonl",

    run:
        adm = geopandas.read_file(str(input), layer=wildcards.level)
        adm.to_file(str(output), driver="GeoJSONSeq", index=False)


rule region_labels:
    input:
        "regions/regions_{layer}.geojsonl",
    output:
        "regions/regions_{layer}_labels.geojsonl",
    shell:
        """
        cat {input} | geojson-polygon-labels > {output} \
            --ndjson \
            --label=polylabel \
            --style=largest \
            --include-minzoom=9-16 \
            --include-area \
            --include-bbox
        """


rule region_mbtiles:
    input:
        "regions/{layer}.geojsonl",
    output:
        "regions/{layer}.mbtiles",
    shell:
        """
        tippecanoe \
            --generate-ids \
            --read-parallel \
            -Bg \
            --full-detail=12 \
            --output="{output}" \
            --layer={wildcards.layer} \
            --force \
            {input}
        """


rule all_region_mbtiles:
    input:
        "regions/regions_level0.mbtiles",
        "regions/regions_level0_labels.mbtiles",
        "regions/regions_level1.mbtiles",
        "regions/regions_level1_labels.mbtiles",
    output:
        "../tileserver/vector/data/regions_level0.mbtiles",
        "../tileserver/vector/data/regions_level0_labels.mbtiles",
        "../tileserver/vector/data/regions_level1.mbtiles",
        "../tileserver/vector/data/regions_level1_labels.mbtiles",
    shell:
        """
        mv regions/regions_*.mbtiles ../tileserver/vector/data/
        """


rule tileserver_vector_config:
    input:
        # expand(
        #     "../tileserver/vector/data/{layer}.mbtiles", layer=network_tilelayers.layer
        # ),
        "../tileserver/vector/data/regions_level0.mbtiles",
        "../tileserver/vector/data/regions_level0_labels.mbtiles",
        "../tileserver/vector/data/regions_level1.mbtiles",
        "../tileserver/vector/data/regions_level1_labels.mbtiles",
    output:
        "../tileserver/vector/config.json",
    run:
        with open(str(output), "w") as fh:
            tileserver_vector_config = {
                "options": {"paths": {"root": "", "fonts": "fonts", "mbtiles": "data"}},
                "styles": {},
            }
            data = {}
            for fname in sorted(input):
                fname = os.path.basename(fname)
                data[fname.replace(".mbtiles", "")] = {"mbtiles": fname}
            tileserver_vector_config["data"] = data

            json.dump(tileserver_vector_config, fh, indent=2)

rule raster_temp_file:
    output:
        "raster/link/{layer}.tif",
    run:
        layer = get_hazard_layer(wildcards.layer, hazard_layers)
        os.symlink(os.path.join(config["analysis_data_dir"], layer.path), str(output))


def get_hazard_layer(layer_name, hazard_layers):
    try:
        return hazard_layers[hazard_layers.key == layer_name].iloc[0]
    except IndexError as e:
        print(f"Could not find {layer_name} in hazard layers.")
        raise e


rule raster_zero_nodata:
    input:
        rules.raster_temp_file.output,
    output:
        "raster/nodata/{layer}.tif",
    shell:
        """
        NODATA=$(gdalinfo "{input}" -json | jq .bands[0].noDataValue)

        # handle case of NODATA == nan - the JSON output of gdalinfo will change
        # nan to "NaN" so we need to reverse that for gdal_calc.py
        if [ "$NODATA" == '"NaN"' ]
        then
          NODATA=nan
        fi

        if [ "$NODATA" == 'null' ]
        then
          NODATA=nan
        fi

        # replace zeros with NoData value
        gdal_calc.py \
          --quiet \
          -A "{input}" \
          --outfile="{output}" \
          --overwrite \
          --calc="numpy.where(A<=0,$NODATA,A)" \
          --NoDataValue=$NODATA \
          --hideNoData || echo "Fail ok"
        """


rule raster_to_cog:
    input:
        rules.raster_zero_nodata.output,
    output:
        "../tileserver/raster/data/{layer}.tif",
    shell:
        """
        # translate to Cloud-Optimised GeoTIFF
        #
        # could use gdalwarp directly - options chosen to match (reasonably
        # closely) the output of `terracotta optimize-rasters`:
        #
        # gdalwarp "{input}" "{output}" \
        #     -t_srs "EPSG:3857" \
        #     -r near \
        #     -of COG \
        #     -co COMPRESS=DEFLATE \
        #     -co BLOCKSIZE=256
        #
        terracotta optimize-rasters \
            -o ../tileserver/raster/data/ \
            --overwrite \
            --reproject \
            --nproc -1 \
            --resampling-method nearest \
            {input}
        """
